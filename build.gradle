buildscript {
  repositories {
    jcenter()
  }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'it.besmartbeopen.gitlab'
version = '0.1.1'

bintray {
  // user/key from gradle.properties
  publications = ['maven']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = 'gitlab-api'
    licenses = ['Apache-2.0']
    issueTrackerUrl = 'https://github.com/besmartbeopen/gitlab-feign-api/issues'
    vcsUrl = 'https://github.com/besmartbeopen/gitlab-feign-api.git'
    labels = ['gitlab', 'rest', 'openfeign']
    publicDownloadNumbers = true
    githubRepo = 'besmartbeopen/gitlab-feign-api'
    githubReleaseNotesFile = 'README.md'

    version {
      name = '0.1.0'
      released  = new Date()
      vcsTag = '0.1.0'
      // attributes = []

      mavenCentralSync {
        sync = false
      }
    }
  }
}

repositories {
  jcenter()
}

configurations {
  optional
  api.extendsFrom optional
}

dependencies {
  compileOnly "org.projectlombok:lombok:1.16.18"

  testCompileOnly "org.projectlombok:lombok:1.16.18"

  api "io.github.openfeign:feign-core:9.5.0"
  api "io.github.openfeign.form:feign-form:2.2.0"
  implementation "com.google.guava:guava:22.0"
  optional "io.github.openfeign:feign-jackson:9.5.0"
  optional "io.github.openfeign:feign-okhttp:9.5.0"
  optional "com.google.inject:guice:4.1.0"

  testImplementation "org.testng:testng:6.9.13.6"
  testImplementation "io.github.openfeign:feign-jackson:9.5.0"
  testImplementation "io.github.openfeign:feign-okhttp:9.5.0"
  // com.squareup.okhttp3 -> logging-interceptor latest.integration
}

test {
  useTestNG()
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourceJar
  archives javadocJar
}

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "marco.andreini"
      name "Marco Andreini"
      email "marco.andrieni@gmail.com"
    }
  }

  scm {
    url "https://github.com/besmartbeopen/gitlab-feign-api"
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'it.besmartbeopen'
      artifactId 'gitlab-feign-api'
      version '0.1.1'
      artifact sourceJar
      artifact javadocJar
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Gitlab REST api client via openfeign')
        root.appendNode('name', 'Gitlab Feign API')
        root.appendNode('url', 'https://github.com/besmartbeopen/gitlab-feign-api')
        root.children().last() + pomConfig
      }
      from components.java
    }
  }
}